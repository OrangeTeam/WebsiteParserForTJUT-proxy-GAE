allprojects {
    group = 'org.orange'
    version = '2.0'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    tasks.withType(JavaCompile) { task ->
        task.options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) { task ->
        task.options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

configure([project(":api"), project(":client")]) {
    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }
    task install(dependsOn: publishToMavenLocal)
}
